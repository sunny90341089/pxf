/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.github.hierynomus.license-report" version "0.15.0"
    id "io.franzbecker.gradle-lombok" version "3.3.0"
    id "org.springframework.boot" version "${spring_boot_version}" apply false
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
}

downloadLicenses {
    dependencyConfiguration = 'bootJar'
}

def javaProjects = subprojects.findAll {
    it.name.startsWith("pxf-")
}

allprojects {
    apply plugin: "jacoco"
    apply plugin: "idea"
    apply plugin: "eclipse"

    repositories {
        jcenter()
    }
}

idea {
    project {
        jdkName = "1.8"
        languageLevel = "8"
        vcs = "Git"
    }
}

configure(javaProjects) {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: "io.franzbecker.gradle-lombok"

    group = 'org.greenplum.pxf'
    version = "${version}"

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${spring_boot_version}")
        }
        dependencies {
            dependency("com.google.guava:guava:20.0")
            dependency("commons-io:commons-io:2.7")
            dependency("commons-lang:commons-lang:2.6")
            dependency("commons-logging:commons-logging:1.1.3")
            dependency("commons-collections:commons-collections:3.2.2")
            dependency("commons-configuration:commons-configuration:1.10")
            dependency("commons-codec:commons-codec:1.14")

            dependency("com.fasterxml.jackson.core:jackson-databind:2.11.0")
            dependency("com.fasterxml.jackson.core:jackson-core:2.11.0")
            dependency("com.fasterxml.jackson.core:jackson-annotations:2.11.0")

            dependency("joda-time:joda-time:2.8.1")

            dependency("org.apache.commons:commons-lang3:3.10")

            dependency("org.slf4j:slf4j-api:1.7.30")
            dependency("org.slf4j:slf4j-log4j12:1.7.30")
        }
    }

    task removeIdeaOutDirectory(type: Delete) {
        file("${projectDir}/out").deleteDir()
    }

    clean {
        dependsOn(removeIdeaOutDirectory)
    }

    compileJava {
        options.compilerArgs += [
                "-g", "-Xlint:varargs", "-Xlint:cast", "-Xlint:classfile", "-Xlint:dep-ann", "-Xlint:divzero",
                "-Xlint:empty", "-Xlint:finally", "-Xlint:overrides", "-Xlint:path", "-Xlint:-processing", "-Xlint:static",
                "-Xlint:try", "-Xlint:fallthrough", "-Xlint:deprecation", "-Xlint:unchecked", "-Xlint:-options", "-Werror"
        ]
    }

    compileTestJava {
        options.compilerArgs += [
                "-g", "-Xlint:varargs", "-Xlint:cast", "-Xlint:classfile", "-Xlint:dep-ann", "-Xlint:divzero",
                "-Xlint:empty", "-Xlint:finally", "-Xlint:overrides", "-Xlint:path", "-Xlint:-processing", "-Xlint:static",
                "-Xlint:try", "-Xlint:fallthrough", "-Xlint:deprecation", "-Xlint:unchecked", "-Xlint:-options", "-Werror"
        ]
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }

        all*.exclude group: 'javax.servlet', module: 'servlet-api'
        all*.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
}

task stage(type: Copy) {
    description "Build PXF server and stage resulting artifacts for packaging."
    dependsOn ":pxf-service:bootJar"

    // Provide the pxf-hbase jar. The pxf-hbase.jar is required for the PXF
    // HBase connector to support filter pushdown
    from("pxf-hbase/build/libs") { into 'lib' }
    from("pxf-service/build/libs") { into 'server' }
    from('pxf-service/src/main/resources/pxf-profiles-default.xml') { into 'conf' }
    from('pxf-service/src/scripts/pxf-env-default.sh') { into 'conf' }
    from('pxf-service/src/scripts/') {
        include 'pxf'
        include 'kill-pxf.sh'
        into 'bin'
        filter { line -> line.replaceAll('_PXF_VERSION_', "${version}") }
    }
    from('pxf-service/src/') { include 'templates/**' }
    into "$buildDir/stage/pxf"

    doLast { new File("${buildDir}/stage/pxf/version").text = "${version}\n" }
}
